# Find install manifest
if(NOT EXISTS "@CMAKE_BINARY_DIR@/install_manifest.txt")
  message(FATAL_ERROR "Cannot find install manifest: @CMAKE_BINARY_DIR@/install_manifest.txt")
endif()

# Stop service
message(STATUS "Stopping hotkey-manager-daemon service (if running)...")
execute_process(COMMAND systemctl stop hotkey-manager-daemon.service OUTPUT_QUIET ERROR_QUIET)
message(STATUS "Disabling hotkey-manager-daemon service (if existing)...")
execute_process(COMMAND systemctl disable hotkey-manager-daemon.service OUTPUT_QUIET ERROR_QUIET)

# Uninstall files
file(READ "@CMAKE_BINARY_DIR@/install_manifest.txt" install_manifest)
string(REPLACE "\n" ";" install_files_list "${install_manifest}")
foreach(install_file ${install_files_list})
    if(EXISTS "${install_file}")
        message(STATUS "Removing installed file: ${install_file}")
        file(REMOVE "${install_file}")
    else()
        message(STATUS "Installed file not found (skipping): ${install_file}")
    endif()
endforeach()

# Clean up header directory if empty
set(_hotkey_include_dir "@CMAKE_INSTALL_PREFIX@/include/hotkey_manager")
if(EXISTS "${_hotkey_include_dir}")
    set(_remove_dir TRUE)
    file(GLOB _top_level_entries LIST_DIRECTORIES TRUE "${_hotkey_include_dir}/*")
    if(_top_level_entries)
        foreach(_entry ${_top_level_entries})
            if(IS_DIRECTORY "${_entry}")
                file(GLOB _nested_entries "${_entry}/*")
                if(_nested_entries)
                    set(_remove_dir FALSE)
                    break()
                endif()
            else()
                set(_remove_dir FALSE)
                break()
            endif()
        endforeach()
    endif()
    if(_remove_dir)
        message(STATUS "Removing directory tree: ${_hotkey_include_dir}")
        file(REMOVE_RECURSE "${_hotkey_include_dir}")
    endif()
endif()
